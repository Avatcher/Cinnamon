plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
    id 'java-library'
    id 'signing'
}

version = "0.1.0"

dependencies {
    api project(":cinnamon-api")
    shadow "dev.jorel:commandapi-bukkit-shade:9.3.0"

    compileOnly 'io.papermc.paper:paper-api:1.20.2-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier = "plugin"
    project.configurations.api.canBeResolved = true
    configurations = [project.configurations.shadow, project.configurations.api]
    relocate("dev.jorel.commandapi", "dev.avatcher.cinnamon.core.commandapi")
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven (MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Cinnamon Core"
                description = "The Minecraft plugin of Cinnamon"
                url = "https://github.com/Avatcher/Cinnamon"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/license/mit"
                    }
                }
                developers {
                    developer {
                        name = "Dmytro Pravdin"
                        email = "pravdindmytro@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/Avatcher/Cinnamon.git"
                    developerConnection = "scm:git:ssh://github.com:Avatcher/Cinnamon.git"
                    url = "https://github.com/Avatcher/Cinnamon/tree/main"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}
